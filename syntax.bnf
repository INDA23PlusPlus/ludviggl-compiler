
<Program> ::= <ToplevelList>;

<ToplevelList> ::= 
    <TopLevel> <ToplevelList> | 
    <TopLevel>;

<TopLevel> ::= <FuncDecl>;

<FuncDecl> ::= "fn" <IDENTIFIER> "(" <ParamList> ")" "=" <Block> ";";

<Block> ::= "{" (StatementList | Empty) "}";

<StatementList> ::= 
    <Statement> <StatementList> | 
    <Statement>;

<Statement> ::=
    <Declaration>     |
    <Assignment>      |
    <IfStatement>     |
    <WhileStatement>  |
    <BreakStatement>  |
    <ReturnStatement> |
    <PrintStatement>;

<Declaration> ::= "let" "IDENTIFIER" "=" <Expression> ";";

<Assignment> ::= "IDENTIFIER" "=" <Expression> ";";

<Expression> ::= 
    <Sum> ("<" | "<=" | "==" | ">=" | ">" | "!=") Expression |
    <Sum>;

<Sum> ::=
    <Product> ("+" | "-") <Sum> |
    <Product>;

<Product> ::=
    <Factor> ("*" | "/" | "%") <Product> |
    <Factor>;

<Factor> ::=
    <Call>               |
    ("-" | "!") <Factor> |
    "IDENTIFIER"         |
    "LITERAL"            |
    "(" <Expression> ")";

<Call> ::= "IDENTIFIER" "(" <ArgList> ")";

<ParamList> ::=
    "IDENTIFIER" "," <ParamList> |
    "IDENTIFIER";

<ArgList> ::=
    <Expression> "," <ArgList> |
    <Expression>;

<IfStatement> ::=
    "if" <Expression> <Block> |
    "if" <Expression> <Block> "else" <Block>;

<WhileStatement> ::= "while" <Expression> <Block>;

<ReturnStatement> ::= "return" <Expression> ";";

<BreakStatement> ::= "break" ";"

<PrintStatement> ::= "print" <Expression> ";"
