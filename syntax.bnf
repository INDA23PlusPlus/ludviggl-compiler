
<Program> ::= <ToplevelList>;

<ToplevelList> ::= 
    <TopLevel> <ToplevelList> | 
    <TopLevel>;

<TopLevel> ::= <FuncDecl>;

<FuncDecl> ::=
    "fn" <IDENTIFIER> "(" <ParamList> ")" ":" <Type> "=" <Block> ";" |
    "fn" <IDENTIFIER> "(" ")" ":" <Type> "=" <Block> ";";

<Block> ::=
    "{" StatementList "}" |
    "{" "}";

<StatementList> ::= 
    <Statement> <StatementList> | 
    <Statement>;

<Statement> ::=
    <Declaration>       |
    <Assignment>        |
    <IfStatement>       |
    <WhileStatement>    |
    <BreakStatement>    |
    <ContinueStatement> |
    <ReturnStatement>   |
    <PrintStatement>;

<Declaration> ::=
    "let" "IDENTIFIER": <Type> "=" <Expression> ";" |
    "let" "IDENTIFIER" "=" <Expression> ";";

<Type> ::= ("int" | "bool");

<Assignment> ::= "IDENTIFIER" "=" <Expression> ";";

<Expression> ::=
    <Equality> ("and" | "or") <Equality> |
    <Equality>;

<Equality> ::=
    <Comparison> "==" <Comparison> |
    <Comparison>; 

<Comparison> ::= 
    <Sum> ("<" | "<=" | "==" | ">=" | ">" | "!=") <Sum> |
    <Sum>;

<Sum> ::=
    <Product> ("+" | "-") <Sum> |
    <Product>;

<Product> ::=
    <Factor> ("*" | "/" | "%") <Product> |
    <Factor>;

<Factor> ::=
    <Call>               |
    ("-" | "!") <Factor> |
    "IDENTIFIER"         |
    "LITERAL"            |
    "true"               |
    "false"              |
    "(" <Expression> ")";

<Call> ::=
    "IDENTIFIER" "(" <ArgList> ")" |
    "IDENTIFIER" "(" ")";

<ParamList> ::=
    <Param>  "," <ParamList> |
    <Param>;

<Param> ::= "IDENTIFIER" ":" <Type>;

<ArgList> ::=
    <Expression> "," <ArgList> |
    <Expression>;

<IfStatement> ::=
    "if" <Expression> <Block> |
    "if" <Expression> <Block> "else" <Block>;

<WhileStatement> ::= "while" <Expression> <Block>;

<ReturnStatement> ::= "return" <Expression> ";";

<BreakStatement> ::= "break" ";"

<ContinueStatement> ::= "continue" ";"

<PrintStatement> ::= "print" <Expression> ";"
